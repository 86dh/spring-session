buildscript {
	ext {
		releaseBuild = version.endsWith('RELEASE')
		snapshotBuild = version.endsWith('SNAPSHOT')
		milestoneBuild = !(releaseBuild || snapshotBuild)
	}

	repositories {
		gradlePluginPortal()
		maven { url 'https://repo.spring.io/plugins-release/' }
        maven {
            url = 'https://repo.spring.io/plugins-snapshot'
            if (project.hasProperty('artifactoryUsername')) {
                credentials {
                    username "$artifactoryUsername"
                    password "$artifactoryPassword"
                }
            }
        }
	}

	dependencies {
		classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
	}
}

plugins {
	id "com.github.ben-manes.versions"
	id "nl.littlerobots.version-catalog-update" version "0.8.1"
}

apply plugin: 'io.spring.convention.root'
apply plugin: 'org.springframework.security.update-dependencies'

group = 'org.springframework.session'
description = 'Spring Session'

subprojects {
	apply plugin: 'io.spring.javaformat'

	plugins.withType(JavaPlugin) {
		sourceCompatibility = JavaVersion.VERSION_17
	}

	tasks.withType(Test) {
		useJUnitPlatform()
	}

	// Spring Framework 6.1 requires -parameters to be able to introspect method parameter names
	tasks.withType(JavaCompile) {
		options.compilerArgs.add("-parameters")
	}
}

nohttp {
    source.exclude "buildSrc/build/**"
	source.exclude "spring-session-docs/.gradle/nodejs/**"
	source.exclude "spring-session-docs/modules/ROOT/examples/**/build/**"
}

tasks.named("updateDependencies") {
	// we aren't Gradle 8 compatible yet
	checkForGradleUpdate = false
}

updateDependenciesSettings {
	gitHub {
		organization = "spring-projects"
		repository = "spring-session"
	}
	dependencyExcludes {
		majorVersionBump()
		minorVersionBump()
		alphaBetaVersions()
		snapshotVersions()
	}
	setFiles({
		return [
				project.file("gradle/dependency-management.gradle"),
				project.file("buildSrc/src/main/groovy/io/spring/gradle/convention/CheckstylePlugin.groovy")
		]
	})
}
